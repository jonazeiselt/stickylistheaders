apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 36
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 36
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.eiselts.stickylistheaders'

    libraryVariants.configureEach { variant ->
        if (variant.name == 'release') {
            tasks.register("generate${variant.name.capitalize()}Javadoc", Javadoc) {
                source = variant.javaCompileProvider.get().source
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                classpath += variant.javaCompileProvider.get().classpath
                exclude '**/R.java'
                exclude '**/BuildConfig.java'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

tasks.register('javadocJar', Jar) {
    dependsOn tasks.named("generateReleaseJavadoc")
    from tasks.named("generateReleaseJavadoc").get().destinationDir
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.github.jonazeiselt'
            artifactId 'stickylistheaders'
            version '1.0.0'
            from components.findByName('release')
            artifact tasks.named('sourcesJar')
            artifact tasks.named('javadocJar')
        }
    }
}
